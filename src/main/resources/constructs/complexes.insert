prefix np: <http://www.nanopub.org/nschema#>
prefix wp: <http://vocabularies.wikipathways.org/wp#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix has-source: <http://semanticscience.org/resource/SIO_000253>
prefix prov: <http://www.w3.org/ns/prov#>
prefix wd: <https://www.wikidata.org/entity/>

insert {
  graph ?headGraph {
    ?nanopub a np:Nanopublication .
    ?nanopub np:hasAssertion ?assertionGraph .
    ?nanopub np:hasProvenance ?provenanceGraph .
    ?nanopub np:hasPublicationInfo ?publicationInfoGraph .
  }
  graph ?assertionGraph	{
    ?complex a wp:Complex ;
      wp:organismName ?organism .
    ?complex wp:participant ?participant .
    ?participant a ?participantType .
  }
  graph ?provenanceGraph {
    ?assertionGraph has-source: ?pathway .
    ?assertionGraph has-source: ?pmidURI .
  }
  graph ?publicationInfoGraph {
    ?nanopub dcterms:created ?now ;
      dcterms:rights <https://creativecommons.org/licenses/by/3.0/> ;
      prov:wasDerivedFrom ?pathway ;
      prov:wasGeneratedBy ?activity .
    ?activity a prov:Activity;
      prov:used <https://github.com/wikipathways/nanopublications> ;
      prov:atLocation wd:Q1137652 .
  }
} where {
  ?complex a wp:Complex ;
    dcterms:isPartOf ?pathway ;
    dcterms:bibliographicCitation ?pmidURI .
  ?pathway a wp:Pathway .
  ?complex wp:participants ?participant . ?participant a ?participantType .
  ?pathway a wp:Pathway ;
    wp:organismName ?organism .
  FILTER (?participantType != wp:DataNode)
  bind(iri(concat(str(?complex),"_nanopub")) as ?nanopub)
  bind(iri(concat(str(?nanopub),"head")) as ?headGraph)
  bind(iri(concat(str(?nanopub),"assertion")) as ?assertionGraph)
  bind(iri(concat(str(?nanopub),"provenance")) as ?provenanceGraph)
  bind(iri(concat(str(?nanopub),"publicationInfo")) as ?publicationInfoGraph)
  bind(now() as ?now)
}
