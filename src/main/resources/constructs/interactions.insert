prefix np: <http://www.nanopub.org/nschema#>
prefix wp: <http://vocabularies.wikipathways.org/wp#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix has-source: <http://semanticscience.org/resource/SIO_000253>
prefix prov: <http://www.w3.org/ns/prov#>
prefix wd: <https://www.wikidata.org/entity/>

insert {
  graph ?headGraph {
    ?nanopub a np:Nanopublication .
    ?nanopub np:hasAssertion ?assertionGraph .
    ?nanopub np:hasProvenance ?provenanceGraph .
    ?nanopub np:hasPublicationInfo ?publicationInfoGraph .
  }
  graph ?assertionGraph	{
    ?interaction a wp:Interaction .
    ?interaction wp:source ?source .
    ?interaction wp:target ?target .
    ?interaction wp:organismName ?organism .
  }
  graph ?provenanceGraph {
    ?assertionGraph has-source: ?pathway .
  }
  graph ?publicationInfoGraph {
    ?nanopub dcterms:created ?now ;
      prov:wasDerivedFrom ?pathway ;
      prov:wasGeneratedBy ?activity .
    ?activity a prov:Activity; 
      prov:used <https://github.com/wikipathways/nanopublications> ;
      prov:atLocation wd:Q1137652 .
  }
} where {
  ?interaction a wp:Interaction ;
    dcterms:isPartOf ?pathway .
  ?interaction wp:source ?source .
  ?interaction wp:target ?target .
  ?pathway a wp:Pathway ;
    wp:organismName ?organism .
  bind(iri(concat(str(?interaction),"_nanopub")) as ?nanopub)
  bind(iri(concat(str(?nanopub),"head")) as ?headGraph)
  bind(iri(concat(str(?nanopub),"assertion")) as ?assertionGraph)
  bind(iri(concat(str(?nanopub),"provenance")) as ?provenanceGraph)
  bind(iri(concat(str(?nanopub),"publicationInfo")) as ?publicationInfoGraph)
  bind(iri(concat(str(?nanopub),"activity")) as ?activity)
  bind(now() as ?now)
}
